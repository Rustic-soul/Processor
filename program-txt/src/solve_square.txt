#------------------------------------------------------#
#                                                      #
#          Решение квадратных уравнений                #                             
#                                                      #
#------------------------------------------------------#


#---------------------------------
# Ввод коэффициентов квадратного уравнения(ax^2 + bx + c = 0)
in
in
in
pop rcx
pop rbx
pop rax

# Ветка на случай [a = 0] 
push rax
push 0
je var_a=0

# Ветка на случай [a != 0; b = 0]
push rbx
push 0
je var_a!=0__b=0

# Ветка на случай [a != 0; b != 0; c = 0]
push rcx
push 0
je var_a!=0__b!=0__c=0

# Ветка на случай [a != 0; b != 0; c != 0]
jmp discriminant

#---------------------------------


#---------------------------------
# Описание случая {a = 0}
:var_a=0

# Ветка на случай [a = 0; b = 0]
push rbx
push 0
je var_a=0__b=0

# Ветка на случай [a = 0; b != 0; c = 0]
push rcx
push 0
je one_solution_equals_0

# Ветка на случай [a = 0; b != 0; c != 0]
jmp var_a=0__b!=0__c!=0
#---------------------------------


#---------------------------------
# Описание случая {a != 0; b = 0}
:var_a!=0__b=0

# Ветка на случай [a != 0; b = 0; c = 0]
push rcx
push 0
je one_solution_equals_0

# Ветка на случай [a != 0; b = 0; c != 0]
jmp discriminant
#---------------------------------


#---------------------------------
# Описание случая {a = 0; b = 0}
:var_a=0__b=0

# Ветка на случай [a = 0; b = 0; c = 0]
push rcx
push 0
je infinite_solutions
 
# Ветка на случай [a = 0; b = 0; c != 0]
jmp not_solutions
#---------------------------------


#---------------------------------
# Описание случая {a = 0; b = 0; c = 0}
:infinite_solutions

# 10 восьмерок как символ бесконечности
push 8888888888
push 8888888888
out
out
hlt
#---------------------------------


#---------------------------------
# Описание случаев {a = 0; b != 0; c = 0} и {a != 0; b = 0; c = 0}
:one_solution_equals_0

push 0
out
hlt
#---------------------------------


#---------------------------------
# Описание случая {a = 0; b != 0; c != 0}
:var_a=0__b!=0__c!=0

# В данном случае x = -c/b
push rcx
push -1
mul

push rbx
div

out
hlt
#---------------------------------


#---------------------------------
# Описание случая {a != 0; b != 0; c = 0}
:var_a!=0__b!=0__c=0

# В данном случае два корня x = 0; x = -b/a
push rbx
push -1
mul

push rax
div

push 0

out
out
hlt
#---------------------------------


#---------------------------------
# Описание случаев {a != 0; b = 0; c != 0} и {a != 0; b != 0; c != 0}
:discriminant

# Подсчет дискриминанта (b^2)
push rbx
push rbx
mul

# Подсчет дискриминанта (4ac)
push 4
push rax
push rcx
mul
mul

# Подсчет дискриминанта (b^2 - 4ac)
sub
pop rdx

# Случай D < 0 (нет корней)
push rdx
push 0
jb not_solutions

# Случай D = 0 (один корень)
push rdx     
push 0
je one_solution

# Самый общий случай с двумя корнями
push rbx
push -1
mul
push rdx
sqrt
add

push rax
push 2
mul
div

push rbx
push -1
mul
push rdx
sqrt
sub

push rax
push 2
mul
div

out
out
hlt
#---------------------------------


#---------------------------------
# Описание случая {a = 0; b = 0; c != 0}
:not_solutions
hlt
#---------------------------------


#---------------------------------
# Описание случаев {a != 0; b = 0; c != 0} и {a != 0; b != 0; c != 0}, когда D = 0 
:one_solution
push rbx
push -1
mul

push rax
push 2
mul

div

out
hlt
#---------------------------------
